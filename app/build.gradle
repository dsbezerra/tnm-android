apply plugin: 'com.android.application'

apply plugin: 'com.apollographql.android'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

def tnmVersionCode = 27
def tnmVersionName = '2.05.01'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    sourceSets.main {
        jniLibs.srcDir 'src/main/jniLibs'
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi', 'armeabi-v7a', 'mips'
            universalApk false
        }
    }
    defaultConfig {
        applicationId "com.tnmlicitacoes.app"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode tnmVersionCode
        versionName tnmVersionName
        multiDexEnabled true
        generatedDensities = []
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
}

ext.versionCodes = [armeabi: 1, 'armeabi-v7a': 2, mips: 3, x86: 4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    // assign different version code for each output
    variant.outputs.each { output ->
        output.versionCodeOverride =
                project.ext.versionCodes.get(output.getFilter(OutputFile.ABI)) * 1000000 + android.defaultConfig.versionCode
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.apollographql.apollo:apollo-android-support:$apolloVersion"
    compile "com.apollographql.apollo:apollo-runtime:$apolloVersion"

    compile "com.evernote:android-state:$evernoteAndroidStateVersion"
    annotationProcessor "com.evernote:android-state-processor:$evernoteAndroidStateVersion"

    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.firebase:firebase-core:$firebaseVersion"

    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:preference-v14:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"


    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.6@aar') {
        transitive = true;
    }
    compile 'com.andkulikov:transitionseverywhere:1.7.3'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-vector-drawable:25.3.1'
    testCompile 'junit:junit:4.12'

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
}

apply plugin: 'com.google.gms.google-services'
